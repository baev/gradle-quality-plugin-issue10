buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.2'
        classpath 'ru.vyarus:gradle-quality-plugin:3.1.0'
    }
}

group 'io.github.baev'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

apply plugin: 'java'

sourceCompatibility = 1.8

apply plugin: 'ru.vyarus.quality'

def generatedSources = 'build/generated-sources/mapstruct'

configurations {
    mapstruct
}

sourceSets {
    main {
        java {
            srcDirs += generatedSources
        }
    }
}

dependencies {
    mapstruct('org.mapstruct:mapstruct-processor:1.2.0.Final')
    compile('org.mapstruct:mapstruct-jdk8:1.2.0.Final')
}

task generateMappers(type: JavaCompile, group: 'build', description: 'generate mappers') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly + configurations.mapstruct
    options.compilerArgs = ["-proc:only"]
    destinationDir = file(generatedSources)
}

compileJava.dependsOn generateMappers

quality {
    excludeSources = fileTree('build/generated-sources')
    pmdVersion = '6.6.0'
}


repositories {
    mavenCentral()
}
